;; lobo: Logo Bolo
;; (c) Ben Kurtovic, 2011

to spawn-player
  create-tanks 1 [
    set player tank who
    set-tank-vars 0 true
  ]
end

to do-player-logic
  if mouse-inside? [
    if mouse-down? and not mouse-was-down? [
      set is-accelerating? true
      set player-has-target? true
      set player-target-xcor (round mouse-xcor)
      set player-target-ycor (round mouse-ycor)
    ]
    set mouse-was-down? mouse-down?
  ]
  
  ifelse player-has-target? [
    tank-facexy player-target-xcor player-target-ycor
    let dist distancexy player-target-xcor player-target-ycor
    if dist < 2 and speed > 0.075 [
      decelerate friction * 5
    ]
    if dist < 0.1 [
      cancel-target
    ]
  ] [
    tank-facexy mouse-xcor mouse-ycor
  ]
end

to cancel-order
  if player-has-target? [
    cancel-target
  ]
end

to cancel-target
  set is-accelerating? false
  set player-has-target? false
end
